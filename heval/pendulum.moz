/*
   Modeling Kernel Language (Modelyze) library
   Copyright (C) 2010-2012 David Broman

   Modelyze library is free software: you can redistribute it and/or modify
   it under the terms of the GNU Lesser General Public License as published by
   the Free Software Foundation, either version 3 of the License, or
   (at your option) any later version.

   Modelyze library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public License
   along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.

   written by Oscar Eriksson oerikss@kth.se
 */

include OSCSIM

// Constants
def pi = 3.1415
def g = 9.81
def l = 3.
def m = 5.
def angle = (45.0)*pi/180.0

def pendulum(x: <Real>, y: <Real>, vx: <Real>, vy: <Real>, T: <Real>) = {
  init x (l*cos(angle));
  init y (l*sin(angle));
  init vx 0.;
  init vy 0.;
  init T 0.;
  -T*x/l = m*(der vx);
  -T*y/l - m*g = m*(der vy);
  vy = der y;
  vx = der x;
  x^2. + y^2. = l^2.;
}

def freefall(x: <Real>, y: <Real>, vx: <Real>, vy: <Real>) = {
  0. = der vx;
  -g = der vy;
  vy = der y;
  vx = der x;
}

def bpendulum(x: <Real>, y: <Real>, vx: <Real>, vy: <Real>, T: <Real>) = {
  sw (pendulum x y vx vy T) (fun t: () -> freefall x y vx vy) (T < 200.)
}

def main = {
  def x, y, vx, vy, T: Real;
  def monitored = [(x, "x"), (y, "y"), (T, "T")];
  def bp = bpendulum x y vx vy T;
  def sr = simulateDebug bp 0. 0.1 10.0 monitored 10;
  print(pprintSimulation (sr))
}
