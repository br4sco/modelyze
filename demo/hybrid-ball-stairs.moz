/*
Modeling Kernel Language (Modelyze) library 
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.
*/


include ModelyzeHC
include Mechanical


def HybridBallWithStairs(m:Real,l:Real,startAngle:Real) = {
  def g = 9.81;
  def x,y:Position;
  def ground:Position;

  def Pendulum:Mode;
  def BouncingBall:Mode;
  def time:Real;
  time' = 1.0;

  init ground (-3.0);
  ground' = 0.;

  hybridchart initmode Pendulum {
      mode Pendulum {
          def T:Force;
          init x (l*sin(startAngle));
          init y (-l*cos(startAngle));

          -T*x/l = m*x'';
          -T*y/l - m*g = m*y'';     
          x^2. + y^2. = l^2.;
          transition BouncingBall when (time > 1.0 && T > 70.)
              action nothing;
    };
    mode BouncingBall {    
        def timer:Signal;
        timer' = 1.0;
        x'' = 0.;
        -m*g = m*y'';
  
        transition BouncingBall when (y <= ground)
            action (y' <- y' * -0.7);

        transition BouncingBall when (timer > 0.3 && ground > -5.0)
            action (timer <- 0. ; ground <- ground - 0.2);
    };
  };

  probe("ground") = ground;
  probe("y") = y;
}  


def main =
  def angle = 45.*3.14/180.0;
  printsim (HybridBallWithStairs 5.0 2.0 angle) 0.01 6.0
