/*
Modeling Kernel Language (Modelyze) library 
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.
*/


include ModelyzeHC
include Mechanical

def g = 9.81

type Pos = <Real>
type Force = <Real>

def PendulumExt(m:Real, l:Real, T:Force, x:Pos, y:Pos) = {
    -T*x/l = m*x'';
    -T*y/l - m*g = m*y''; 
    x^2. + y^2. = l^2.;
}


def BreakingPendulum(m:Real, l:Real, angle:Real) = {
    def x,y:Position;
    def time:Real;
    def Pendulum, BouncingBall:Mode;
    init x (l*sin(angle));
    init y (-l*cos(angle));
    time' = 1.0;
    probe("y") = y;

    hybridchart initmode Pendulum {   
        mode Pendulum {
            def T:Force;
            probe("T") = T;
            PendulumExt(m, l, T, x, y);
            transition BouncingBall 
                when (time >= 3.5 && T >= 4.0) action nothing;
        }; 
        mode BouncingBall {
            x'' = 0.;
            -g = y'';
            transition BouncingBall 
                when (y <= -4.0) action (y' <-  y' * -0.7);
        };
    };
}



def main = {
    def angle = 120.*3.14/180.;
    def model = BreakingPendulum 0.13 2. angle;
    printsim model 0.02 20.;
}











