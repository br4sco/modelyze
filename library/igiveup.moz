/*
Modeling Kernel Language (Modelyze) library
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.

written by Oscar Eriksson oerikss@kth.se
*/

include Equation
include ModelyzeDAE

def pre : <Real> -> <Real>
def (<=) : <Real> -> <Real> -> Equations
type DAE = Equations
type DAC = Equations
type CDAE = Equations

type TopolElem
def topol : <Real> -> <Real> -> Dyn -> Dyn -> TopolElem

type MModel
def switch : MModel -> <Real> -> (() -> MModel) -> MModel
def init : <Real> -> <Real> -> MModel
def model: Equations -> MModel 
def top :  TopolElem -> MModel
def (--) : MModel -> MModel -> MModel 

type State = (<Real> => Real)
type Topology = Set(TopolElem)
type Interval = (Real, Real)
type MState = (MModel, State, State, Interval)

// ======= HELPERS =======

def _state_check_elem(v: <Real>) -> () = {
  match v with
  | der x -> (
      match x with
      | sym: Real -> ()
      | _ -> error "state only allows state variables"
    )
  | sym: Real -> ()
  | _ -> error "state only allows state variables"
}

def state_find(v: <Real>, z: State) -> Real = {
  def _ = _state_check_elem v;
  if (Map.mem v z) then (Map.find v z) else def _ = dprint(v); error " :undefined state variable"
}

// Test
// def x, y: <Real>
// def _ = state_find (x + y) (Map.empty) // state only allows state variables
// def _ = state_find (der (der x)) (Map.empty) // state only allows state variables

def state_mem(v: <Real>, z: State) -> Bool = {
  def _ = _state_check_elem(v);
  Map.mem v z
}

def _print_sim_headers(labels: [String]) -> () = {
  def _ = print "time";
  def _ = map (fun l: String -> print ("\t" ++ l)) labels;
  print "\n"
}

def _print_time(t: Real, n: Int) -> () = {
  def _ = print (real2string t);
  if n != -1 then def _ = print(", "); print(n)
  else ()
}

def _print_sim_results(isDefined: (<Real> -> Bool), ev: (<Real> -> Real), probes: [<Real>]) -> () = {

  def f(e: <Real>) -> () = {
    def _ =  print "\t";
    if isDefined e then print (real2string (ev e))
    else print "?"
  };

  def _ = map f probes;
  print "\n"
}

def _upd_arr(x: <Real>, z: State, i: Int, a: {Real}) -> () = {
  if !(state_mem x z) then ()
  else (Array.set a i (state_find x z))
}

def _dae_state_to_ukmap_yy_yp(d: DAE, z: State, udfv: Real) -> (UkMap, {Real}, {Real}) = {
  def (n, ukm) = realUnknowns d;
  def yy = (Array.make n udfv);
  def yp = (Array.make n udfv);
  def _ = map
    (fun e: (<Real>, (Int, Bool)) ->
      { def (x, (i, _)) = e; _upd_arr x z i yy })
    (Map.toList ukm);
  def _ = map
    (fun e: (<Real>, (Int, Bool)) ->
      { def (x, (i, _)) = e; _upd_arr (der x) z i yp })
    (Map.toList ukm);
  (ukm, yy, yp)
}

def eVMapToUkMapYy(d: DAE, z: State, udfv: Real) -> (UkMap, {Real}) = {
  def (n, ukm) = realUnknowns d;
  def yy = (Array.make n udfv);
  def _ = map
    (fun e: (<Real>, (Int, Bool)) ->
      { def (x, (i, _)) = e; _upd_arr x z i yy })
    (Map.toList ukm);
  (ukm, yy)
}

def _ukmap_yy_yp_to_state(ukm: UkMap, yy: {Real}, yp: {Real}) -> State = {
  def f(acc: State,  e: (<Real>, (Int, Bool))) -> State = {
    def (x, (i, _)) = e;
    Map.add x (Array.get yy i) (Map.add (der x) (Array.get yp i) acc)
  };

  foldLeft f (Map.empty) (Map.toList ukm)
}

def ukMapYyToVMap(ukm: UkMap, yy: {Real}) -> State = {
  def f(acc: State,  e: (<Real>, (Int, Bool))) -> State = {
    def (x, (i, _)) = e;
    Map.add x (Array.get yy i) acc
  };

  foldLeft f (Map.empty) (Map.toList ukm)
}

def _get_DAESolver_input(dae: DAE, dac: DAC, z_0: State, t_0: Real, undefval: Real)
  -> (Residual, Int, Residual, {Real}, {Real}, {Real}, UkMap) = {

  def (ukm, yy, yp) = _dae_state_to_ukmap_yy_yp dae z_0 undefval;
  
  def mkRootFuns(ukm: UkMap, dac: Equations, t :Real, yy:{Real}, yp:{Real}) -> [Real] = {
    def eval(e: <?>) = evalFromArrays(e,ukm,yy,yp);
    def work (d: Equations, acc: [Real]) -> [Real] = {
      match d with
      | e1; e2 -> work e2 (work e1 acc)
      | e1 <= e2 -> (eval(e2) - eval(e1))::acc
      | _ -> acc
    };
    work dac [];
  };

  def vids = initConditionCorrection ukm;
  def resf = makeResidual ukm dae;
  def rootf = mkRootFuns ukm dac;
  def nroots = length (rootf 0. yy yp);
  (resf, nroots, rootf, yy, yp, vids, ukm)
}

// ======= SEMANTICS ==========

def indexred(d: DAE) -> DAE = {
  def (dr, dmap) = indexReducationPantelides d (Map.empty);
  dr
}

def eval_expr(e: <Real>, z: State) -> Real = {
  def work(e: <?>) -> ? = {
    match e with
    | pre _ -> error "pres not allowed in expr"
    | der x -> state_find e z
    | sym: Real -> state_find e z
    | f a -> (work f) (work a)
    | lift v: ? -> v
    | _ -> error "unsupported model construct"  
    };
  work e    
}

// Test
// def x, y: <Real>
// def z = listToMap [(x, 1.), (y, 2.), (der x, 3.)]
// def _ = dprint(eval_expr (x + y * x / y) z) // 2
// def _ = dprint(eval_expr (x + y * (der x) / y) z) // 4

def cdae_to_dae(cdae: Equations) -> Equations = {
  match cdae with
  | d_1; d_2 -> (cdae_to_dae d_1); (cdae_to_dae d_2)
  | lhs = rhs -> cdae
  | _ -> NoEqn
}

// Test
// def x, y: <Real>
// def _ = dprint(cdae_to_dae (x = y; x <= y; x + y = 2.))
// x_1 = y_1;
// NoEqn_1;
// x_1 + y_1 = sval(2)

def cdae_to_dac(c: Equations) -> Equations = {
  match c with
  | d_1; d_2 -> (cdae_to_dac d_1); (cdae_to_dac d_2)
  | lhs <= rhs -> c
  | _ -> NoEqn
}

// Test
// def x, y: <Real>
// def _ = dprint(cdae_to_dac (x = y; x <= y; x + y = 2.))
// NoEqn_1;
// x_1 <= y_1;
// NoEqn_1

def stable(d: Equations, z: State) -> Bool = {
  match d with
  | d_1; d_2 -> (stable d_1 z) && (stable d_2 z)
  | lhs <= rhs -> (eval_expr lhs z) < (eval_expr rhs z)
  | _ -> error "expected a DAC in stable"
}

// Test
// def x, y: <Real>
// def z = listToMap [(x, 2.), (y, 3.)]
// def _ = dprint(stable (x <= y;1. <= y) z) // true
// def _ = dprint(stable (x <= y;4. <= y) z) // false
// def _ = dprint(stable (x <= y;1. = y) z) // expected a DAC in stable

def consistent_w_dae(d: Equations, z: State) -> Bool = {
  def epsilon = 1e-8;
  match d with
  | d_1; d_2 -> (consistent_w_dae d_1 z) && (consistent_w_dae d_2 z)
  | lhs = rhs -> abs((eval_expr lhs z) - (eval_expr rhs z)) < epsilon
  | _ -> error "expected a DAE in consistent_w_dae"
}

// Test
// def x, y: <Real>
// def z = listToMap [(x, 2.), (y, 1.)]
// def _ = dprint(consistent_w_dae (x = 2. * y;1. = y) z) // true
// def _ = dprint(consistent_w_dae (x = 2. * y;4. = y) z) // false
// def _ = dprint(consistent_w_dae (x <= y;1. = y) z) // expected a DAE in consistent_w_dae

def solve(cdae: CDAE, z_0: State, I: Interval, h: Real, probes: [<Real>]) -> (Interval, State, State) = {

  // Step taken when finding consistent initial values
  def dt = 0.00001;

  def (t_0, t_f) = I;

  // Preapare for solving
  def dae = indexred (cdae_to_dae cdae);
  def dac = (cdae_to_dac cdae);

  // Setup the DAE Solver
  def (resf, nroots, rootf, yy1, yp1, vids, ukm) = _get_DAESolver_input dae dac z_0 t_0 0.;
  def yy0 = (Array.make (Array.length yy1) INFINITE);
  def yp0 = (Array.make (Array.length yp1) INFINITE);
  def t0 = INFINITE;
  def s = (DAESolver.initWithRootf resf nroots rootf t_0 yy1 yp1);
  def _ = (DAESolver.setStopTime s t_f);
  def _ = (DAESolver.calcIcYaYdp s vids yy1 yp1 (t_0 + dt));

  // Function for printing simulation results
  def psr(yy: {Real}, yp: {Real}, t: Real) = {
    def _ = _print_time t (-1);
    _print_sim_results (fun v: <Real> -> Map.mem v ukm) (fun v: <Real> -> evalFromArrays v ukm yy yp) probes
  };

  // And then solve!
  def go(sr: SolverResult, t0: Real, yy0: {Real}, yp0: {Real},
    t1: Real, yy1: {Real}, yp1: {Real})
      -> (SolverResult, Real, {Real}, {Real}, {Real}, {Real}) = {

    def find_left_limit(nyy0: {Real}, nyp0: {Real}) -> Real = {
      def e = 1.e-10;
      def _ = (DAESolver.reinit s t0 yy0 yp0);

      def work(t: Real) -> Real = {
        if (t1 - t > e) then {
          def (tc, sr) = (DAESolver.solveNormal s t nyy0 nyp0);
          if sr != DAE_SOLVER_RESULT_ROOTS_FOUND then {
            work (t + (t1 - t) / 2.)
          } else {
            def _ = (DAESolver.reinit s t0 yy0 yp0);
            def (pt, _) = (DAESolver.solveNormal s (2. * t - t1) nyy0 nyp0);
            pt
          }
        } else t
      };

      work (t0 + (t1 - t0) / 2.)
    };

    if sr == DAE_SOLVER_RESULT_SUCCESS then {
      def _ = psr yy1 yp1 t1;
      def (nt0, nsr) = (DAESolver.solveNormal s (t1 + h) yy0 yp0);
      go nsr t1 yy1 yp1 nt0 yy0 yp0
    } else {
      if sr == DAE_SOLVER_RESULT_ROOTS_FOUND then {
        def nyy0 = (Array.make (Array.length yy0) 0.);
        def nyp0 = (Array.make (Array.length yp0) 0.);
        def nt0 = find_left_limit nyy0 nyp0;
        def _ = psr nyy0 nyp0 nt0;
        (sr, t1, nyy0, nyp0, yy1, yp1)
      } else {
        def _ = psr yy1 yp1 t1;
        (sr, t1, yy0, yp0, yy1, yp1)
      }
    }
  };

  def (_, t_fp, yy0p, yp0p, yy1p, yp1p) =
    go DAE_SOLVER_RESULT_SUCCESS t0 yy0 yp0 t_0 yy1 yp1;
  def z_ll = _ukmap_yy_yp_to_state ukm yy0p yp0p;
  def z_0p = _ukmap_yy_yp_to_state ukm yy1p yp1p;
  ((t_0, t_fp), z_ll, z_0p)
}

// Test
// def x: <Real>
// def z_0 = listToMap [(x, 0.)]
// def cdae = der x = 1.;x <= 3.
// def _ = dprint(solve cdae z_0 (0., 10.) 0.1 [x])

def eval_mexpr(e: <Real>, z_ll: State, z_0: State) -> Real = {
  def work(e: <?>) -> ? = {
    match e with
    | pre x -> eval_expr x z_ll
    | der x -> eval_expr e z_0
    | sym: Real -> eval_expr e z_0
    | f a -> (work f) (work a)
    | lift v: ? -> v
    | _ -> error "unsupported model construct"  
    };
  work e    
}

// Test
// def x, y: <Real>
// def z_0 = listToMap [(x, 1.), (der x, 2.), (y, 1.)]
// def z_ll = listToMap [(x, 2.)]
// def _ = dprint(eval_mexpr (der x + 1. - (pre x) * y) z_0 z_ll)
