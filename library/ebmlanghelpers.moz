/*
Modeling Kernel Language (Modelyze) library
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.

written by Oscar Eriksson oerikss@kth.se
*/

include ModelyzeDAE
type VMap = (<Real> => Real)
type ZCMap = (<Real> => Int)
type T = (Real, Int)
type F = (Set(Equations), VMap, Real)
type M = (Equations, F, Set(<Real>), T)

def EPSILON = 1.0e-10

def is_equal(lhs: <Real>, rhs: <Real>) -> Bool =
  (Set.mem rhs (Set.add lhs (Set.empty)))

def is_equal(lhs: Set(?), rhs: Set(?)) -> Bool = {
  def subsetleft = forAll (fun e: ? -> (Set.mem e rhs)) (Set.toList lhs);
  def subsetright = forAll (fun e: ? -> (Set.mem e lhs)) (Set.toList rhs);
  subsetleft && subsetright
}

def eval_from_vmap(a: <?>, mu: VMap) -> Real = {
  def checkUndef = (fun x: <Real> -> if (Map.mem x mu) then ()
    else error "undefined variable");

  def yyfun(x: <Real>) -> <Real> = { def _ = checkUndef x; (Map.find x mu) };
  def ypfun(x: <Real>) -> <Real> = { error "we do not allow derivatives in vmap" };
  eval a yyfun ypfun
}

def eval_from_vmap_with_default_val(expr: <Real>, mu: VMap, default: Real)
  -> Real = {

  def undefined_vars_in(expr: <?>) -> Bool = {
    match expr with
    | sym: Real -> !(Map.mem expr mu)
    | der x -> !(Map.mem expr mu)
    | f e -> (undefined_vars_in f) || (undefined_vars_in e)
    | lift v: ? -> false
    | _ -> error "Unmatched pattern in undefined_vars_in"
  };

  if undefined_vars_in expr then default
  else eval_from_vmap expr mu
}

def eval_from_arrays_with_default_val(expr: <Real>, ukm: UkMap, yy: VarArray,
  yp: VarArray, default: Real) -> Real = {

  def undefined_vars_in(expr: <?>) -> Bool = {
    match expr with
    | sym: Real -> !(Map.mem expr ukm)
    | der x -> undefined_vars_in x
    | f e -> (undefined_vars_in f) || (undefined_vars_in e)
    | lift v: ? -> false
    | _ -> error "Unmatched pattern in undefined_vars_in"
  };

  if undefined_vars_in expr then default
  else evalFromArrays expr ukm yy yp
}

def cont_to_set(cont: ?) -> Set(?) = {
  def work(cont: ?, acc: Set(?)) -> Set(?) = {
    match cont with
    | this; that -> work that (work this acc)
    | last -> (Set.add last acc)
  };
  work cont (Set.empty)
}

def cont_to_list(cont: ?) -> [?] = {
  reverse (Set.toList (cont_to_set cont))
}

def set_to_cont_impl(set: Set(?)) -> ? = {
   def work(l: [?], acc: ?) -> ? = {
      match l with
      | e::es -> work es (acc;e)
      | [] -> acc
   };
   def l = (Set.toList set);
   if (length l) == 0 then error "Empty set" else
   if (length l) == 1 then (hd l) else work (tl l) (hd l)
}

def set_to_cont(set: Set(?)) -> ? = {
  set_to_cont_impl set
}

def set_to_cont(set: Set(Equations)) -> Equations = {
  if (Set.size set) == 0 then NoEqn else set_to_cont_impl set
}

def E_vmap_to_ukmap_yy_yp(E: Set(Equations), vmap: VMap, undefval: Real) ->
  (UkMap, VarArray, VarArray) = {
  def llog = logger "E_vmap_to_ukmap_yy_yp" DEBUG;
  def (n, ukm) = realUnknowns (set_to_cont E);
  def yy = (Array.make n undefval);
  def yp = (Array.make n undefval);
  def work(gvlist: [(<Real>, Real)]) -> () = {
    def get_uk_and_arr(e: <Real>) -> (<Real>, VarArray) = {
      match e with
      | der x -> (x, yp)
      | x -> (x, yy)
      | _ -> error "expected variables and derivatives"
    };
    match gvlist with
    | e::es -> {
        def (ee, v) = e;
        def (x, a) = get_uk_and_arr ee;
        def (i, _) = {
            def _ = llog "variable:" x;
            if (Map.mem x ukm) then (Map.find x ukm)
            else error "expected to find var in ukm"
          };
        def _ = (Array.set a i v);
        work es
      }
    | [] -> ()
  };

  def _ = work (Map.toList vmap);
  (ukm, yy, yp)
}

def ukmap_yy_yp_to_vmap(ukm: UkMap, yy: VarArray, yp: VarArray) -> VMap = {
  def f(acc: VMap,  e: (<Real>, (Int, Bool))) -> VMap = {
    def (x, (i, _)) = e;
    Map.add x (Array.get yy i) (Map.add (der x) (Array.get yp i) acc)
  };
  foldLeft f (Map.empty) (Map.toList ukm)
}

def get_dae_solver_input(f: F, L: Set(<Real>), undefval: Real)
  -> (Residual, Int, Residual, VarArray, VarArray, VarArray, UkMap, ZCMap) = {

  def makeRootFun(zcbl: [<Real>], ukm: UkMap, t: Real, yy: VarArray,
    yp: VarArray)-> [Real] = {

    def f = (fun e: <Real> -> (evalFromArrays e ukm yy yp) - EPSILON);
    map f zcbl
  };

  def makeZCMap(zcbl: [<Real>]) -> ZCMap = {
    def f(i: Int, e: <Real>) -> (<Real>, Int) = { (e, i) };
    listToMap (mapi f zcbl)
  };

  def zcbl = (Set.toList L);
  def (E, vmap, t) = f;
  def (ukm, yy, yp) = E_vmap_to_ukmap_yy_yp E vmap undefval;
  def vids = initConditionCorrection ukm;
  def resf = makeResidual ukm (set_to_cont E);
  def rootf = makeRootFun zcbl ukm;
  def zcm = makeZCMap zcbl;
  def nroots = (Set.size L);
  (resf, nroots, rootf, yy, yp, vids, ukm, zcm)
}

def get_dae_find_ic_input(f: F, undefval: Real) ->
  (Residual, VarArray, VarArray, VarArray, UkMap) = {
  def (resf, _, _, yy, yp, vids, ukm) =
    get_dae_solver_input f (Set.empty) undefval;
  (resf, yy, yp, vids, ukm)
}

def makeBEulerFuns(eqs: Equations, ukm: UkMap, h: Real, yy0: {Real}, yy: {Real})
  -> [Real] = {

  def yyfun(x: <Real>) -> <Real> = { Array.get yy (fst (Map.find x ukm)) };
  def ypfun(x: <Real>) -> <Real> = {
    def i = (fst (Map.find x ukm));
    ((Array.get yy i) - (Array.get yy0 i)) / h
  };

  def f(e: Equations) -> Real = {
    match e with
    | e1 = e2 -> (eval e1 yyfun ypfun) - (eval e2 yyfun ypfun)
    | _ -> error "Expected only equations here"
  };

  map f (cont_to_list eqs)
}

// def interpolateOverZeroCrossing(zcm: ZCMap, zca: {Root}, ukm: UkMap,
//   t0: Real, yy0: {Real}, yp0: {Real}, t1: Real, yy1: {Real}, yp1: {Real}) -> Real = {

//   def f(a0: {Real}, a1: {Real}, t: Real, x: <Real>) -> <Real> = {
//     def i = (fst (Map.find x ukm));
//     def x0 = (Array.get a0 i);
//     def x1 = (Array.get a1 i);
//     (x1 - x0) / (t1 - t2) * (t - t0) + x0
//   };

//   def yyfun(t: Real, x: <Real>) = f yy0 yy1;
//   def ypfun(t: Real, x: <Real>) = f yp0 yp1;

//   def candidates =
//    map
//      (fun e: (<Real>, Int) -> fst e)
//      (filter
//        (fun b: (<Real>, Int) -> { def (e, i) = b; (Array.get zca i) != 0 })
//        (Map.toList zcm));

//   def sysf(ts: {Real}) -> [Real] = {
//     def f(i: Int, c: <Real>) -> Real = {
//       def t = (Array.get ts i);
//       eval c (yyfun t) (ypfun t)
//     };

//     mapi f candidates
//   };

//   def tt = (Array.mk )
// }
