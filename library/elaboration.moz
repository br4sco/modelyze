/*
Modeling Kernel Language (Modelyze) library 
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.
*/



include Equation
include Physical 

type Node = <>
type Unknown = <>
type Branches = <>
type NodeMap = (Node => Unknown)
type NodeMapList = [(Node,Unknown)]
type BSet = (Set Branches)   //Set of branches
type BList = [Branches]      //List of branches
type ExprMap = (Node => Expr)
type ExprList = [(Node, Expr)]

type MultiNodeMap = (Node => [Node])

type SubExprMap = (<> => Signal)
type SubExprList = [(<>,Signal)]


def addNode(node:Node, nodemap:NodeMap) -> NodeMap = {
    if Map.mem node nodemap then nodemap
    else {
      def u:<Real>;
      Map.add node u nodemap;
    };
}

def sumexpr(branches:BSet) -> ExprMap = {
  def worker(branches:BList, emap:ExprMap) -> ExprMap = {
    match branches with
    | (Branch i v p n)::bs -> {
        def emap1 = {
           if Map.mem p emap 
           then Map.add p ((Map.find p emap) + i) emap
           else Map.add p i emap
        };
        def emap2 = {
           if Map.mem n emap1 
           then Map.add n ((Map.find n emap1) - i) emap1
           else Map.add n (-i) emap1;
        };
        worker(bs,emap2);
      }
    | (RefBranch i v p)::bs -> {
        def emap1 = {
            if Map.mem p emap 
            then Map.add p ((Map.find p emap) + i) emap
            else Map.add p i emap
        };
        worker bs emap1;
      }
    | [] -> emap
  };
  worker(Set.toList branches,Map.empty);
}


def sumzero(m:Equations, branches:BSet) -> Equations = {
  def worker(elist:ExprList) -> Equations = {
    match elist with
    | (_,e)::es -> e = 0.; worker es
    | [] -> m
  };
  worker(Map.toList (sumexpr(branches)));
}


def potentials(m:Equations)->(Equations,BSet) = {
  def worker(m:Equations, nodemap:NodeMap, branchset:BSet) -> (<>,NodeMap,BSet) = {
    match m with
    | Branch i v p n -> {
        def nodemap2 = addNode(n,addNode p nodemap);
        def eq = (v = (Map.find p nodemap2) - (Map.find n nodemap2));
        (eq,nodemap2,Set.add m branchset);
      }
    | RefBranch i v p -> {
        def nodemap2 = addNode(p,nodemap);
        def eq = (v = (Map.find p nodemap2));
        (eq,nodemap2,Set.add m branchset);
      }
    | e1 ; e2 -> {
        def (e1b,nodemap1,branchset1) = worker(e1,nodemap,branchset);
        def (e2b,nodemap2,branchset2) = worker(e2,nodemap1,branchset1);
        (e1b ; e2b, nodemap2, branchset2);
      }
    | _ ->  (m,nodemap,branchset) 
  };
  def (model,_,branchset) = worker(m,Map.empty,Set.empty);
  (model,branchset);
}


def addMultiNode(node:Node, length:Int, nodemap:MultiNodeMap) -> MultiNodeMap = {
    if Map.mem node nodemap then nodemap
    else {
      def makeNodes(n:Int) -> [Node] = {
          def node:<Real>;
          if n == 0 then [] else node::makeNodes(n-1); 
      };
      Map.add node (makeNodes(length)) nodemap;
    };
}

def makeBranches(i_lst:[<Real>],v_lst:[<Real>],p_lst:[<Dyn>],n_lst:[<Dyn>]) -> Equations = {
    match (i_lst,v_lst,p_lst,n_lst) with
    |(i::[],v::[],p::[],n::[]) -> Branch(i,v,p,n) 
    |(i::is,v::vs,p::ps,n::ns) -> Branch(i,v,p,n) ; makeBranches(is,vs,ps,ns)
    | _ -> error "Incorrect number of elements when generating multi branches." 
}

def makeRefBranches(i_lst:[<Real>],v_lst:[<Real>],p_lst:[<Dyn>]) -> Equations = {
    match (i_lst,v_lst,p_lst) with
    |(i::[],v::[],p::[]) -> RefBranch(i,v,p) 
    |(i::is,v::vs,p::ps) -> RefBranch(i,v,p) ; makeRefBranches(is,vs,ps)
    | _ -> error "Incorrect number of elements when generating multi reference branches." 
}

def expandMultiBranches(model:Equations) -> Equations = {
    def expand(m:Equations, mnodemap:MultiNodeMap) -> (Equations,MultiNodeMap) = {     
        match m with
        | MultiBranch (sval i_lst:[<Real>]) (sval v_lst:[<Real>]) p n -> {
            def len = length(i_lst);
            if len != length(v_lst)
            then error "Multi branches need to have the same dimentions."
            else{
                def map2 = addMultiNode(p,len,addMultiNode(n,len,mnodemap));  
                (makeBranches(i_lst,v_lst, Map.find p map2, Map.find n map2), map2);
            }
          }
        | MultiRefBranch (sval i_lst:[<Real>]) (sval v_lst:[<Real>]) p -> {
            def len = length(i_lst);
            if len != length(v_lst)
            then error "Multi reference branches need to have the same dimentions."
            else{
                def map2 = addMultiNode(p,len,mnodemap);  
                (makeRefBranches(i_lst,v_lst, Map.find p map2), map2);
            }
          }
        | e1 ; e2 -> {
            def (e1b,map2) = expand(e1,mnodemap);
            def (e2b,map3) = expand(e2,map2);
            (e1b ; e2b, map3);
          }
        | _ -> (m,mnodemap)
    };
    def (eqs,_) = expand(model,Map.empty);
    eqs;
}

 
def elaborateConnections(model:Equations) -> Equations = {
     def (model2,branchset2) = potentials(expandMultiBranches(model));
     sumzero(model2,branchset2);
}



