/*
Modeling Kernel Language (Modelyze) library
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.

written by Oscar Eriksson oerikss@kth.se
*/

include Equation
include Graph
include Vector

type Terminal = (Vector, Vector)
type BranchChordTrans = (Vector, Vector)
type BaseTrans = (Vector, Vector)
type SubsMap = (Vector => Vector)
type Var2AccThrSpaceMap = (Vector => [Vector])
type ConstrVar = (Vector, [Vector])
type OrthoFormul = ([Vector], Vector)
type OrthoFormulMatrix = (MatrixMapSReal, MatrixMapSReal)
type BaseVectorMatrixPair = (Vector, MatrixMapSReal)
type BaseVector2MatrixMap = (Vector => MatrixMapSReal)
type BaseVector2NodeMap = (Vector => Node)
type Node2BaseMap = (Node => Base)
type NodeBasePair = (Node, [BaseVectorMatrixPair])
type ModelEdge =
  ((NodeBasePair, NodeBasePair), [Terminal], [ConstrVar], [ConstrVar])
type Domain = [ModelEdge]
type XY = (MatrixMapVector, MatrixMapVector)
type TopolInfo = (MatrixMapReal, XY)
type IncidenceMatrix = MatrixMapReal
type Var2VarsMap = (Vector => Set(Vector))

def mkCutsetMatrix(IM: IncidenceMatrix, XY: XY) -> TopolInfo = {
  def (X, Y) = XY;
  if rdim X == cdim IM && rdim Y == cdim IM then {
    def work(i: Int, a: (MatrixMapInt, MatrixMap, MatrixMap))
      -> (MatrixMapInt, MatrixMap, MatrixMap) = {

      def (M, X, Y) = a;
      if i > rdim M then a
      else {
        def j_max = snd (argfind (fun v: Int -> v != 0)
                      (sub (i, 1) (i, cdim M) M));
        if i == j_max then work (i + 1) a
        else {
          work (i + 1) (swcols i j_max M,
                        swrows i j_max X,
                        swrows i j_max Y)
        }
      }
    };

    def IMrrt = trimzerorows (rowreduce IM);
    if rdim IMrrt > cdim IMrrt then error BAD_MATRIX
    else {
      def (IA, nX, nY) = work 1 (real2int IMrrt, X, Y);
      def (I, A) = vsplit (rdim IA) IA;
      (int2real A, (nX, nY))
    }
  } else error "Dimensions of variable matrices and incidence matrix does not agree"
}

def substitute(terminals: SubsMap, branchchords: SubsMap, xys: [XY])
  -> [XY] = {

  def subs(xy: XY) -> XY = {

    def work(y: [Vector], x: [Vector])
      -> ([Vector], [Vector]) = {

      // def _ = dprint("=== SUBS using TERMINALS ===");

      def y = map (fun t: Vector -> subs t terminals) y;
      def x = map (fun a: Vector -> subs a terminals) x;

      // def _ = dprint("y = ");
      // def _ = dprint(y);
      // def _ = dprint("x = ");
      // def _ = dprint(x);

      // def _ = dprint("=== SUBS using TRANS MAPS ===");

      def y = map (fun t: Vector -> subs t branchchords) y;
      def x = map (fun a: Vector -> subs a branchchords) x;

      // def _ = dprint("y = ");
      // def _ = dprint(y);
      // def _ = dprint("x = ");
      // def _ = dprint(x);

      if(
          (exists (fun t: Vector -> canSub t terminals) y) ||
          (exists (fun a: Vector -> canSub a terminals) x) ||
          (exists (fun t: Vector -> canSub t branchchords) y) ||
          (exists (fun a: Vector -> canSub a branchchords) x)
        ) then work y x
      else (y, x)
    };

    def (x, y) = xy;
    if dim x != dim y || cdim x != 1 then error BAD_MATRIX
    else {
      def (xl, yl) = work (concat (matrixToList (tr x)))
                            (concat (matrixToList (tr y)));

      (tr (listToMatrix [xl] (tr x)), tr (listToMatrix [yl] (tr y)))
    }
  };
  map subs xys
}

def getTopolInfo(Ds: [Domain]) -> [TopolInfo] = {
  def getTopolInfo(D: Domain) -> TopolInfo = {

    def G =
      map (fun me: ModelEdge -> def ((n1, _), (n2, _)) = fst me; (n1, n2)) D;

    def IM = mkIncidenceMatrix G;
    def m = cdim IM;

    def Xl = map (fun cvs: [ConstrVar] -> map (fun cv: ConstrVar -> fst cv) cvs)
              (map (fun me: ModelEdge -> def (_, _, cvs, _) = me; cvs) D);

    def nx = length (hd Xl);
    def X = listToMatrix Xl (mmVectorZero (m, nx));

    def Yl = map (fun cvs: [ConstrVar] -> map (fun cv: ConstrVar -> fst cv) cvs)
              (map (fun me: ModelEdge -> def (_, _, _, cvs) = me; cvs) D);

    def ny = length (hd Yl);
    def Y = listToMatrix Yl (mmVectorZero (m, ny));

    mkCutsetMatrix IM (X, Y)
  };

  map (fun D: Domain -> getTopolInfo D) Ds
}

def mkBase2MatrixMap(Ds: [Domain]) -> BaseVector2MatrixMap = {
  listToMap (concat (concat (map
    (fun D: Domain ->
    (map (fun me: ModelEdge ->
          def ((_, bvmp1), (_, bvmp2)) = fst me; append bvmp1 bvmp2) D)) Ds)))
}

def mkBase2NodeMap(Ds: [Domain]) -> BaseVector2NodeMap = {
  listToMap (concat (concat (map
    (fun D: Domain ->
    (map (fun me: ModelEdge ->
          def ((n1, bvmp1), (n2, bvmp2)) = fst me;
          def bs1 = fst (unzip bvmp1);
          def bs2 = fst (unzip bvmp2);
          append (map (fun b: Vector -> (b, n1)) bs1)
                 (map (fun b: Vector -> (b, n2)) bs2)
         ) D)) Ds)))
}

def mkNode2BaseMap(Ds: [Domain]) -> Node2BaseMap = {
  listToMap (concat (concat (map
    (fun D: Domain ->
    (map (fun me: ModelEdge ->
          def ((n1, bvmp1), (n2, bvmp2)) = fst me;
          def bs1 = fst (unzip bvmp1);
          def bs2 = fst (unzip bvmp2);
          [(n1, bs1), (n2, bs2)]
         ) D)) Ds)))
}

def mkTerminalSubs(Ds: [Domain]) -> SubsMap = {
  listToMap (concat
              (concat (map (fun D: Domain ->
                (map (fun me: ModelEdge -> snd me) D)) Ds)))
}

def mkVar2AccThrSpaceMap(Ds: [Domain]) -> Var2AccThrSpaceMap = {
  listToMap
    (concat (concat
     (map (fun D: Domain ->
           map (fun me: ModelEdge ->
                def (_, _, xcvs, ycvs) = me;
                append xcvs ycvs
               ) D
          ) Ds)))
}

def mkBranchChordSubs(Ts: [TopolInfo]) -> SubsMap = {
  def mkBranchChordSubs(T: TopolInfo) -> [BranchChordTrans] = {
    def (A, (X, Y)) = T;
    def j = rdim A;
    def (Y_b, Y_c) = hsplit j Y;
    def (X_b, X_c) = hsplit j X;
    def CT = zip (concat (matrixToList (tr Y_b)))
                 (concat (matrixToList (tr (-A * Y_c))));
    def BT = zip (concat (matrixToList (tr X_c)))
                 (concat (matrixToList (tr ((tr A) * X_b))));
    (append CT BT)
  };

  listToMap
    (concat (map (fun T: TopolInfo -> mkBranchChordSubs T) Ts))
}

def mkCircCutRes(Ts: [TopolInfo]) -> [XY] = {
  def f(T: TopolInfo) -> XY = {
    def (A, (x, y)) = T;
    def circM = (vconcat (-(tr A)) (mmRealId (cdim A)));
    def cutM = (vconcat (mmRealId (rdim A)) A);
    (circM * x, cutM * y)
  };
  map f Ts
}

def getZerothDerivs(XYs: [XY]) -> [XY] = {
  map (fun XY: XY ->
        (fst (vsplit 1 (fst XY)), fst (vsplit 1 (snd XY)))) XYs
}

def getHighestDerivs(XYs: [XY]) -> [XY] = {
  map (fun XY: XY ->
        def (X, Y) = XY;
        def nX = cdim X;
        def nY = cdim Y;
        (if nX > 1 then snd (vsplit (nX - 1)  X) else X,
         if nY > 1 then snd (vsplit (nY - 1)  Y) else Y)) XYs
}

def getBranchChordVars(Ts: [TopolInfo]) -> [XY] = {  map (fun T: TopolInfo ->
       def (A, (x, y)) = T;
       def j = rdim A;
       def (y_b, y_c) = hsplit j y;
       def (x_b, x_c) = hsplit j x;
       (x_b, y_c)
  ) Ts
}

def mkOrthoCoeffs(xbycs: [XY], ccress: [XY]) -> [XY] = {

  map (fun e: (XY, XY) ->
       def ((x_b, y_c), (circeqs, cuteqs)) = e;
       (hconcat x_b y_c, hconcat cuteqs circeqs)
  ) (zip xbycs ccress)
}

def mkOrthoFormul(vatsm: Var2AccThrSpaceMap, ocs: [XY]) -> [OrthoFormul]  = {
  concat
  (map (fun vp: XY ->
        zip (map (fun v: Vector ->
                  Map.find v vatsm) (concat (matrixToList (tr (fst vp)))))
            (concat (matrixToList (tr (snd vp))))
      ) ocs)
}

def filterOutNullSpace(ofs: [OrthoFormul]) -> [OrthoFormul] = {
  filter (fun pf: OrthoFormul -> !(isEmpty (fst pf))) ofs
}

def mkOrthoFormulMatrix(bmm: BaseVector2MatrixMap, bnm: BaseVector2NodeMap,
  nbm: Node2BaseMap, ofs: [OrthoFormul]) -> [OrthoFormulMatrix] = {
  def f(pf: OrthoFormul) -> OrthoFormulMatrix = {
    def (bs, res) = pf;
    def n = (Map.find (hd bs) bnm);
    def base = listToSet (Map.find n nbm);
    def ff(b: Vector) -> MatrixMapSReal = {
      if (Set.mem b base) && (Map.mem b bmm) then Map.find b bmm
      else error "Expression expressed in different bases"
    };

    (vconcat (map (fun b: Vector -> vectorToMatrix ff b) bs),
     vectorToMatrix ff res)
  };
  map f ofs
}

def mkDynRes(ofms: [OrthoFormulMatrix]) -> [Real] = {
  concat (map (fun ofm: OrthoFormulMatrix ->
       concat (matrixToList ((tr (fst ofm)) * (snd ofm)))
      ) ofms)
}

def mkVar2VarsMap(subs: SubsMap) = {
  def f(a: Set(Vector), v: Vector) -> Set(Vector) = {
    Set.add v a
  };
  map (fun e: (Vector, Vector) -> (fst e, foldLeft f (Set.empty) (snd e)))
    (Map.toList subs)
}


// def chgBase(nbm: Node2BaseMap, bnm: BaseVector2NodeMap, etms: [Edge2BaseTransMap], Ts: [AdjList], n: Node, ve: Vector) = {
//   def base = listToSet (Map.find n nbm);
//   def f(v: Vector) -> Vector = {
//     if (Set.mem v base) then v else v
//   };
//   map f ve
// }

// def planarTrans(nbm: Node2BaseMap, Ts: [AdjList], sn: Node, en: Node)

// = {

// }

def formulateRes(Ds: [Domain]) = {
  def terminals = mkTerminalSubs Ds;
  def Ts = getTopolInfo Ds;
  def (As, XYs) = unzip Ts;

  def _ = dprint("=== TopolInfo ===");
  def _ = map dp As;
  def _ = map dp (fst (unzip XYs));
  def _ = map dp (snd (unzip XYs));

  def branchchords = mkBranchChordSubs Ts;
  def xys = getZerothDerivs XYs;
  def xys_star = substitute terminals branchchords xys;
  def ccress = mkCircCutRes (zip As xys_star);

  def _ = dprint("=== X* and Y* ===");
  def _ = map dp (fst (unzip xys_star));
  def _ = map dp (snd (unzip xys_star));

  // def _ = dprint("=== Circ and Cut Eqs ===");
  // def _ = map dp (fst (unzip ccress));
  // def _ = map dp (snd (unzip ccress));

  def h_xys = getHighestDerivs XYs;
  def xbycs = getBranchChordVars (zip As h_xys);

  // def _ = dprint("=== x_b and y_c ===");
  // def _ = map dp (fst (unzip xbycs));
  // def _ = map dp (snd (unzip xbycs));

  def ocs = mkOrthoCoeffs xbycs ccress;
  def vatsm = mkVar2AccThrSpaceMap Ds;
  def ofs = mkOrthoFormul vatsm ocs;

  def nnofs = filterOutNullSpace ofs;

  // def _ = dprint("=== Ortho Formulation  ===");
  def bmm = mkBase2MatrixMap Ds;
  def nbm = mkNode2BaseMap Ds;
  def bnm = mkBase2NodeMap Ds;
  def ofms = mkOrthoFormulMatrix bmm bnm nbm nnofs;

  // def _ = dprint(ofs);
  // def _ = dprint(nnofs);

  def _ = dprint("=== Residual ===");
  def dres = mkDynRes ofms;
  def _ = dprint(dres);
  ()
}
