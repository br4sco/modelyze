/*
Modeling Kernel Language (Modelyze) library
Copyright (C) 2010-2012 David Broman

Modelyze library is free software: you can redistribute it and/or modify
it under the terms of the GNU Lesser General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Modelyze library is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License
along with Modelyze library.  If not, see <http://www.gnu.org/licenses/>.

written by Oscar Eriksson oerikss@kth.se
*/

include EXPORTMATRIX

def SCENE_FILENAME = "/tmp/scene.json"

type Color = String

type Material
def stdMaterial: Color -> Real -> Material

type Geometry3D
def cuboid: Real -> Real -> Real -> Geometry3D
def sphere: Real -> Geometry3D
def cylinder: Real -> Real -> RealVector3 -> Geometry3D

type VisualData
def invisible: VisualData
def visualSolid: Geometry3D -> Material -> VisualData
def visualAxes: Real -> VisualData

type ModelElement
def (++) : ModelElement -> ModelElement -> ModelElement
def visualElement: SRMatrix -> SRMatrix -> VisualData -> ModelElement
def equationsElement: Equations -> ModelElement
def NoElement: ModelElement

type FrameFun1 = Frame -> ModelElement
type FrameFun2 = Frame -> Frame -> ModelElement

def inParallel(l: FrameFun2, r: FrameFun2,
  f_1: Frame, f_2: Frame) -> ModelElement = {

  l f_1 f_2 ++ r f_1 f_2
}
def (||) = inParallel

def inSeries(l: Frame, r: FrameFun1) -> ModelElement = {
  r l
}
def (--) = inSeries

def inSeries(l: FrameFun1, r: Frame) -> ModelElement = {
  l r
}
def (--) = inSeries

def inSeries(f: Frame, r: FrameFun2) -> FrameFun1 = {
  r f
}
def (--) = inSeries

def inSeries(l: FrameFun1, r: FrameFun2) = {
  fun f_2: Frame ->
      def f_1: Frame;
      l f_1 ++ r f_1 f_2
}
def (--) = inSeries

def inSeriesExposeFrame(l: FrameFun2, f_2: Frame) = {
  (l, f_2)
}
def (-.-) = inSeriesExposeFrame

def inSeriesExposeFrame(lf_2: (FrameFun2, Frame), r: FrameFun2) = {
  fun f_1: Frame -> fun f_3: Frame ->
      def (l, f_2) = lf_2;
      l f_1 f_2 ++ r f_2 f_3
}
def (-.-) = inSeriesExposeFrame

def inSeries(l: FrameFun2, r: FrameFun2) = {
  fun f_1: Frame -> fun f_3: Frame ->
    def f_2: Frame;
    l f_1 f_2 ++ r f_2 f_3
}
def (--) = inSeries

def serializei(f: (Int -> FrameFun2), n: Int) -> FrameFun2 = {
  def work(a: FrameFun2, i: Int) -> FrameFun2 = {
    if n - i < 0 then error "n is not a positive number"
    else if n - i == 0 then a
    else work (a -- f (i + 1)) (i + 1)
  };
  work (f 1) 1
}

def serialize(f: FrameFun2, n: Int) -> FrameFun2 = {
  serializei (fun i: Int -> f) n
}

def WHITE = "#ffffff"
def RED = "#ff0000"
def BLUE = "#0000ff"
def GREEN = "#00ff00"
def BLACK = "#000000"

def real2JSONstring(r: Real) -> String = {
  if int2real (real2int r) == r then real2string r ++ "0"
  else real2string r
}

def geometry2JSON(g: Geometry3D) -> String = {
  match g with
  | cuboid (sval w: Real) (sval h: Real) (sval d: Real) ->
      "{ " ++
      "\"type\": \"Cuboid\", " ++
      "\"w\": " ++ real2JSONstring w ++ ", " ++
      "\"h\": " ++ real2JSONstring h ++ ", " ++
      "\"d\": " ++ real2JSONstring d ++
      " }"

  | sphere (sval r: Real) ->
    "{ " ++
    "\"type\": \"Sphere\", " ++
    "\"radius\": " ++ real2JSONstring r ++
    " }"

  | cylinder (sval r: Real) (sval h: Real) (sval u: RealVector3) -> {
      def (u1, u2, u3) = u;
      "{ " ++
      "\"type\": \"Cylinder\", " ++
      "\"radius\": " ++ real2JSONstring r ++ ", " ++
      "\"height\": " ++ real2JSONstring h ++ ", " ++
      "\"u1\": " ++ real2JSONstring u1 ++ ", " ++
      "\"u2\": " ++ real2JSONstring u2 ++ ", " ++
      "\"u3\": " ++ real2JSONstring u3 ++
      " }"
    }

  | _ -> error "Unmatched Geometry3D"
}

def material2JSON(m: Material) -> String = {
  match m with
  | stdMaterial (sval s: String) (sval o: Real) -> {
      if o >= 0. && o <= 1. then {
        "{ " ++
        "\"type\": \"Standard\", " ++
        "\"color\": \"" ++ s ++ "\", " ++
        "\"opacity\": " ++ real2JSONstring o ++
        " }"
      } else error INVALID_ARGUMENT
    }
  | _ -> error "Unmatched Material"
}

def visualData2Json(d: VisualData) -> String = {
  match d with
  | visualSolid g m -> "{ \"type\": \"Solid\"," ++
                       "\"geometry\": " ++ geometry2JSON g ++ ", " ++
                       "\"material\": " ++ material2JSON m ++
                       " }"
  | visualAxes (sval s: Real) -> "{ \"type\": \"Axes\", " ++
                                 "\"size\": " ++ real2JSONstring s ++
                                 " }"
  | _ -> error "Unmatched visual data"
}

def visualElement2Json(o: ModelElement) -> String = {
  def work(o: ModelElement) -> String = {
    match o with
    | o_1 ++ o_2 -> work o_1 ++ ", " ++ work o_2
    | visualElement (sval v: SRMatrix)  (sval q: SRMatrix) vd -> {
        def (x, y, z) = toRealVector3 v;
        def (w, i, j, k) = toRealQauternion q;
        "{ " ++
        "\"xLabel\": \"" ++ symstr(x) ++ "\", " ++
        "\"yLabel\": \"" ++ symstr(y) ++ "\", " ++
        "\"zLabel\": \"" ++ symstr(z) ++ "\", " ++
        "\"wLabel\": \"" ++ symstr(w) ++ "\", " ++
        "\"iLabel\": \"" ++ symstr(i) ++ "\", " ++
        "\"jLabel\": \"" ++ symstr(j) ++ "\", " ++
        "\"kLabel\": \"" ++ symstr(k) ++ "\", " ++
        "\"data\": " ++ visualData2Json vd ++
        " }"
      }
    | _ -> { def _ = dprintln o; error "Expected only visualElement" }
  };
  "{ \"scene\": [ " ++ work o ++ " ] }"
}

def getVisibleVisualElements(o: ModelElement) -> ModelElement = {
  def work(o: ModelElement, a: ModelElement) -> ModelElement = {
    match o with
    | o_1 ++ o_2 -> work o_2 (work o_1 a)
    | visualElement _ _ invisible -> a
    | visualElement _ _ _ -> a ++ o
    | _ -> a
  };
  work o NoElement
}

def cleanupModelElements(o: ModelElement) -> ModelElement = {
    match o with
    | o_1 ++ o_2 -> {
          match (cleanupModelElements o_1, cleanupModelElements o_2) with
          | (NoElement, NoElement) -> NoElement
          | (NoElement, o) -> o
          | (o, NoElement) -> o
          | (o_1b,o_2b) -> o_1b ++ o_2b
       }
    | _ -> o
}

def getModel(o: ModelElement) -> Equations = {
  def work(o: ModelElement, a: Equations) -> Equations = {
    match o with
    | o_1 ++ o_2 -> (work o_2 (work o_1 a))
    | equationsElement e -> a; e
    | _ -> a
  };
  work o NoEqn
}

def elaborateToFile(exportfun: (Equations -> ()), o: ModelElement) -> () = {
  def m = elaborate (getModel o);
  def vo = getVisibleVisualElements o;
  def vo = cleanupModelElements vo;

  def _ = exportfun m;
  match vo with
  | NoElement -> ()
  | _ -> writeFile SCENE_FILENAME (visualElement2Json vo ++ "\n")
}

def elaborateToFile(o: ModelElement) -> () =
  elaborateToFile export2ModelicaFile o

def elaborateToFileDebug(o: ModelElement) -> () =
  elaborateToFile export2ModelicaFileDebug o

def clock1(t0: Real, t: <Real>) -> ModelElement = equationsElement (clock t0 t)

def translationActuator1(f: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (translationActuator f f1 f2)
}

def torqueActuator1(f: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame -> equationsElement (torqueActuator f f1 f2)
}

def translationSensor1(x: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (translationSensor x f1 f2)
}

def rotationSensor1(q: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (rotationSensor q f1 f2)
}

def _mkBodyVarT(t: ()) = {
  def xm1, xm2, xm3: Real;
  mkVector3 xm1 xm2 xm3
}

def _mkBodyVarR(t: ()) = {
  def qm1, qm2, qm3, qm4: Real;
  mkQuaternion qm1 qm2 qm3 qm4
}

def _mkBodyVars(t: ()) = {
  def x = _mkBodyVarT ();
  def q = _mkBodyVarR ();
  (x, q)
}

def mkTestVars(t: ()) = {
  def xm1_test, xm2_test, xm3_test,
    qm1_test, qm2_test, qm3_test, qm4_test: Real;

  def x = mkVector3 xm1_test xm2_test xm3_test;
  def q = mkQuaternion qm1_test qm2_test qm3_test qm4_test;
  (x, q)
}

def body1(world: Frame, m: Real, Jd: RealVector3, vd: VisualData,
  x: SRMatrix, q: SRMatrix) -> FrameFun1 = {

  fun fm: Frame ->
    equationsElement (world -- body m Jd x q -- fm) ++ visualElement x q vd
}

def body1(world: Frame, m: Real, Jd: RealVector3, vd: VisualData,
  q0: RealQuaternion) -> FrameFun1 = {

  fun fm: Frame ->
    def (x, q) = _mkBodyVars ();
    body1 world m Jd vd x q fm ++
    equationsElement (norm2 q ~= 1.; q <~~ mkQuaternion q0)
}

def body1(world: Frame, m: Real, Jd: RealVector3, vd: VisualData)
  -> FrameFun1 = {

  fun fm: Frame ->
    def (x, q) = _mkBodyVars ();
    body1 world m Jd vd x q fm ++ equationsElement (norm2 q ~= 1.)
}

def gravity1(world: Frame, m: Real) -> FrameFun1 = {
  fun fm: Frame -> equationsElement (gravity m world fm)
}

def _mkTransSensVar(t: ()) = {
  def xs1, xs2, xs3: Real;
  def x = mkVector3 xs1 xs2 xs3;
  x
}

def sphericalJoint1(world: Frame, vd: VisualData) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    def x = _mkTransSensVar ();
    def q = mkQuaternion ();
    equationsElement (
                      sphericalJoint f1 f2
                      ; translationSensor x world f1
                      ; q == mkQuaternion idq
                      ; q <~~ mkQuaternion idq
                     )
    ++ visualElement x q vd
}

def sphericalJoint1(world: Frame, r: Real, c: Color, o: Real) -> FrameFun2 = {
  sphericalJoint1 world (visualSolid (sphere r) (stdMaterial c o))
}

def revoluteJoint1(world: Frame, u: RealVector3, vd: VisualData,
  q0: RealQuaternion) -> FrameFun2 = {

  fun f1: Frame -> fun f2: Frame ->
    def qrj1, qrj2, qrj3, qrj4: Real;
    def q = mkQuaternion qrj1 qrj2 qrj3 qrj4;
    def x = _mkTransSensVar ();
    equationsElement (
                      sphericalJoint f1 f2
                      ; translationSensor x world f1
                      ; revoluteJoint u q world f1 f2
                      ; q <~~ mkQuaternion q0
                     )
    ++ visualElement x q vd
}

def revoluteJoint1(world: Frame, r: Real, h: Real, c: Color, o: Real,
  u: RealVector3, q0: RealQuaternion) -> FrameFun2 = {

  revoluteJoint1 world u (visualSolid (cylinder r h u) (stdMaterial c o)) q0
}

def rotationDamper1(D: Real) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame -> equationsElement (damperR D f1 f2)
}

def _mkRigidArmVar(t: ()) = {
  def qra1, qra2, qra3, qra4: Real;
  def q = mkQuaternion qra1 qra2 qra3 qra4;
  q
}

def rigidArm1(world: Frame, r: RealVector3, q: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (rigidArm r q world f1 f2)
}

def rigidArm1(world: Frame, r: RealVector3, q0: RealQuaternion) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    def q = _mkRigidArmVar ();
    rigidArm1 world r q f1 f2 ++
    equationsElement (q <~~ mkQuaternion q0; norm2 q ~= 1.)
}

def rigidArm1(world: Frame, r: RealVector3) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    def q = _mkRigidArmVar ();
    rigidArm1 world r q f1 f2 ++ equationsElement (norm2 q ~= 1.)
}

def bar1(world: Frame, m: Real, w: Real, h: Real, d: Real, c: Color, o: Real,
  xm: SRMatrix, qm: SRMatrix, ql: SRMatrix, qr: SRMatrix) -> FrameFun2 = {

  fun fw: Frame -> fun fe: Frame ->
    def fm: Frame;
    def Jd = mkCuboidInertia m w h d;
    // def Jd = (1., 2., 3.);
    def larm = rigidArm1 world (-w / 2., 0., 0.) qr;
    def rarm = rigidArm1 world (-w / 2., 0., 0.) ql;
    fw -- larm  -.- fm -.- rarm -- fe
    ++ body1 world m Jd (visualSolid (cuboid w h d) (stdMaterial c o)) xm qm fm
    ++ gravity1 world m fm

}

def bar1(world: Frame, m: Real, w: Real, h: Real, d: Real, c: Color, o: Real,
  x: SRMatrix, q: SRMatrix) -> FrameFun2 = {

  fun fw: Frame -> fun fe: Frame ->
    def qr = _mkRigidArmVar ();
    def ql = _mkRigidArmVar ();
    bar1 world m w h d c o x q ql qr fw fe
    ++ equationsElement (qr~== q; ql ~== q)
}

def bar1(world: Frame, m: Real, w: Real, h: Real, d: Real, c: Color, o: Real,
  q0: RealQuaternion) -> FrameFun2 = {

  fun fw: Frame -> fun fe: Frame ->
    def qr = _mkRigidArmVar ();
    def ql = _mkRigidArmVar ();
    def (x, q) = _mkBodyVars ();
    // bar1 world m w h d c o x q ql qr fw fe
    bar1 world m w h d c o x q fw fe
    ++ equationsElement (norm2 q ~= 1. ; q <~~ mkQuaternion q0
                         // ; norm2 ql ~= 1. ; ql <~~ mkQuaternion q0
                         // ; norm2 qr ~= 1. ; qr <~~ mkQuaternion q0
                        )
}

def bar1(world: Frame, m: Real, w: Real, h: Real, d: Real, c: Color, o: Real)
  -> FrameFun2 = {

  fun fw: Frame -> fun fe: Frame ->
    def (x, q) = _mkBodyVars ();
    bar1 world m w h d c o x q fw fe ++ equationsElement (norm2 q ~= 1.)
}

def sphere1(world: Frame, m: Real, r: Real, c: Color, o: Real,
  x: SRMatrix, q: SRMatrix) -> FrameFun1 = {

  fun fm: Frame ->
    def Jd = mkSphericalInertia m r;
    body1 world m Jd (visualSolid (sphere r) (stdMaterial c o)) x q fm
    ++ gravity1 world m fm
}

def translationalSpring1(k: Real, s: Real) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (translationalSpring k s f1 f2)
}

def translationalDamper1(d: Real) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (translationalDamper d f1 f2)
}

def rotationalSpring1(world: Frame, k: Real, u: RealVector3,
  q0: RealQuaternion) -> FrameFun2 = {

  fun f1: Frame -> fun f2: Frame ->
    def q = mkQuaternion ();
    equationsElement (
                      rotationalSpring k u q world f1 f2
                      // rotationalSpringEAA k q world f1 f2
                      ; norm2 q ~= 1.
                      ; q <~~ mkQuaternion q0)
}

def angularVelocityActuator1(f: SRMatrix) -> FrameFun2 = {
  fun f1: Frame -> fun f2: Frame ->
    equationsElement (angularVelocityActuator f f1 f2)
}
